(* ledger grammar file *)
(* vim: set ft=ebnf : *)

<journal> = ledger_entry+ | epsilon

<ledger_entry> = comment
                 | include
                 | account_definition
                 | commodity_definition
                 | commodity_conversion
                 | commodity_price
                 | transaction

(* meta *)
<nl> = <#"\n+">
<white> = <#"[ \t]+">
<text> = #"\S?[^\n]*\S"
<int2> = #"\d{2}"
<int4> = #"\d{4}"

(* dates *)
date = int4 <date_sep> int2 <date_sep> int2 <white?>
date_sep = '/' | '-' | '.'
time = int2 ':' int2 [':' int2]
date_time = date <('t'|'T'|' ')> time

(* numbers *)
amount = #"-?([0-9]|[1-9][0-9]+)(\.|,)[0-9]+"
percentage = amount <'%'>

(* comments & includes *)
(* TODO *)
<comment> = <(';'|'#'|'*')> <text> nl |
            <'comment'> (white|nl) {#".*" nl?} <'end comment'>
include = <'include'> <white> text <nl>

(* standard metadata *)
tag_name = #"[a-zA-Z0-9-]+"
meta_tag = <':'> tag_name <':'> | tag_name <': '> text
meta_directive = <'note'> <white> meta_tag
note_directive = <'note'> <white> !meta_tag text

(* commodity definitions *)
commodity_code = '$' | #"[a-zA-Z][a-zA-Z_]*" | <'"'> #"[a-zA-Z][a-zA-Z0-9_]*" <'"'>
commodity_definition = <'commodity'> <white> commodity_code nl commodity_directive*
<commodity_directive> = white (commodity_format | commodity_option | meta_directive | note_directive) nl
commodity_format = <'format'> <white> text
commodity_option = 'nomarket' | 'default'

(* quantities, conversions, and prices *)
quantity = '0' | (commodity_code <white?> amount) | (amount <white?> commodity_code)
commodity_conversion = <'C'> <white> quantity <white> <'='> <white> quantity nl
commodity_price = <'P'> <white> (date | date_time) <white> commodity_code <white> quantity nl

(* account references *)
<account_name> = #"[^: \n]+( [^: \n]+)*"
<account_path> = account_name (<':'> account_name)*
account = account_path
virtual_account = <'('> account <')'>
balanced_virtual_account = <'['> account <']'>

(* account definitions *)
account_definition = <'account'> <white> account_path nl account_directive*
<account_directive> = white (account_alias_directive | account_assertion | meta_directive | note_directive) nl
account_alias_directive = <'alias'> <white> account
account_assertion = <'assert'> <white> text

(* transactions *)
transaction = date
              [<white> status]
              [<white> code]
              <white> !status !code payee nl
              posting+

status = '!' | '*'
code = <'('> #"[^)]+" <')'>
payee = text

(* Postings *)
posting = <white> [status <white>] account posting_amount? <nl>
posting_amount = <' '> <' '+>
                 [quantity
                   [<white> posting_lot_cost [<white> posting_lot_date]]
                   [<white> posting_price]]
                 [<' '*> posting_balance]

(* TODO *)
amount_expr = quantity | <'('>  <')'>
<posting_account> = account | virtual_account | balanced_virtual_account
posting_lot_cost = <'{'> quantity <'}'>
posting_lot_date = <'['> date <']'>
posting_price = <'@'> <white> quantity
posting_balance = <'='> <white> quantity
posting_date = <'['> <'='?> date <']'>
posting_comment = !posting_date text
